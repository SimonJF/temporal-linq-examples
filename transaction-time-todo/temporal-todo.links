# Imports
import Mvu;
import MvuHTML;
import MvuAttrs;

# Database Definitions
typename Item = (id: Int, done: Bool, content: String);

var db = database "timedb";

var todoTbl =
  table "todo"        # Table name
    with Item         # Type of items in the table 
    where id readonly # ID is read only -- allows "insert returning"
    using transaction_time(recorded_from, recorded_to)
  from db; # TT Table, specifying fields

# Server functions
fun newItem(content) server {
  var id = tt_insert todoTbl values [(=content, done = false)] returning id;
  (=id, =content, done = false)
}

fun updateItem(id, done, content) server {
  update (x <-t- todoTbl)
    where (x.id == id)
    set (=done, =content)
}

fun deleteItem(id) server {
  delete (x <-t- todoTbl)
    where (x.id == id)
}

fun getAt(date) {
  var tbl = ttAt(todoTbl, date);
  query {
    for (x <-- tbl)
      [x]
  }
}

fun getCurrent() {
  var tbl = ttCurrent(todoTbl);
  query {
    for (x <-- tbl)
      [x]
  }
}

fun getTimes() {
  var times =
    query {
      for (x <-t- todoTbl)
        [ttFrom(x)]
    };
  var times = sortBy(fun(x) { x }, times);
  map(show, times)
}

# Hack: Still need to implement "now()" and date formatting on the client.
fun serverNow() server {
  show(now())
}

# MVU Definitions

typename Model = (items: [Item], newItem: String, times: [String]);
typename ID = Int;

typename Message =
  [| UpdateBox:String | Submit | ToggleDone:ID | Delete:ID
   | SubmitTime:String | SubmitNow |];

fun initialModel(items) {
  (=items, newItem="", times = getTimes())
}

fun updt(msg, model) {
  switch (msg) {
    case UpdateBox(str) -> (model with newItem=str)
    case ToggleDone(id) ->
      var items =
        map(fun(x) {
          if (x.id == id) {
            updateItem(x.id, not(x.done), x.content);
            (x with done = not(x.done))
          } else { x }
        }, model.items);
      (model with =items, times = model.times ++ [serverNow()])
    case Delete(id) ->
      var items = filter(fun(x) { x.id <> id }, model.items);
      deleteItem(id);
      (model with =items, times = model.times ++ [serverNow()])
    case Submit ->
      var item = newItem(model.newItem);
      (model with newItem = "",
       items = model.items ++ [item],
       times = model.times ++ [serverNow()])
    case SubmitTime(time) ->
      redirect("/home?at=" ^^ time);
      model
    case SubmitNow ->
      redirect("/home");
      model
  }
}

fun view(model) {
  open MvuAttrs;
  open MvuHTML;

  var a0 = MvuAttrs.empty;
  var h0 = MvuHTML.empty;

  fun displayItem(item) {
    var checkedAttr = checked(item.done);
    var text =
      if (item.done) {
        s(a0, textNode(item.content))
      } else {
        textNode(item.content)
      };

    var deleteButton =
      button(type("button") +@ class("btn btn-danger") +@ onClick(fun() { Delete(item.id) }), textNode("Delete"));

    tr(a0,
      td(a0,
        input(type("checkbox") +@
            checkedAttr +@
            onChange(fun(_) { ToggleDone(item.id) }), h0)) +*
      td(a0, label(a0, text)) +*
      td(a0, deleteButton))
  }

  var renderedItems =
    table_(class("table"),
      MvuHTML.concatMap(displayItem, model.items));

  var newItemBox =
    div(class("form-inline"),
      div(class("form-group mx-sm-8 mb-2"),
        input(class("form-control") +@ type("text") +@ onInput(fun(str) { UpdateBox(str) }) +@ value(model.newItem), h0)) +*
      div(class("form-group mx-sm-3 mb-2"),
        button(type("button") +@ class("btn btn-primary") +@ onClick(fun() { Submit }), textNode("New item"))));

  var timeTravelBox = {
    fun renderTime(x) {
      tr(a0,
        a(href("javascript:void()") +@ onClick(fun() { SubmitTime(x) }), textNode(x)))
    }
    var times = 
      tr(a0, a(href("javascript:void()") +@ onClick(fun() { SubmitNow }), textNode("Now"))) +*
      MvuHTML.concatMap(renderTime, model.times);

    div(class("form-group"), table_(class("table"), times))
  };
 
  h1(a0, textNode("To-do")) +*
  renderedItems +*
  newItemBox +*
  timeTravelBox
}

fun mainPage() {
  var model =
    switch (lookup("at", environment())) {
      case Just(date) -> initialModel(getAt(parseDate(date)))
      case _ -> initialModel(getCurrent())
    };
  Mvu.runSimple("placeholder", model, view, updt);
  page
    <html>
      <head>
        <link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.4.1/css/bootstrap.min.css" integrity="sha384-Vkoo8x4CGsO3+Hhxv8T/Q5PaXtkKtu6ug5TOeNV6gBiFeWPGFN9MuhOf23Q9Ifjh" crossorigin="anonymous"/>
      </head>
      <body>
        <div class="container">
          <div id="placeholder"></div>
        </div>
      </body>
    </html>
}

serveThis(mainPage)
